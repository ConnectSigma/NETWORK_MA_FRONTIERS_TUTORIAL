model{
for(i in 1:NS){ 
    w[i,1] <-0
	  delta[i,t[i,1]]<-0
	  mu[i] ~ dnorm(0,.0001)                                           # vague priors for  baselines
	  for(k in 1:na[i]){ 
	      r[i,k] ~ dbin(p[i,t[i,k]],n[i,k])                                 # binomial likelihood
		  	logit(p[i,t[i,k]])<-mu[i] + delta[i,t[i,k]] 
       }                                                                    # model
		for(k in 2:na[i]){
        delta[i,t[i,k]] ~ dnorm(md[i,t[i,k]],taud[i,t[i,k]])             # trial-specific LOR distributions
        md[i,t[i,k]] <-  d[t[i,k]] - d[t[i,1]]  + sw[i,k]                   # mean of LOR distributions
        taud[i,t[i,k]] <- tau *2*(k-1)/k                                    #precision of LOR distributions
        w[i,k] <- (delta[i,t[i,k]]  - d[t[i,k]] + d[t[i,1]])          #adjustment, multi-arm RCTs
        sw[i,k] <-sum(w[i,1:(k-1)])/(k-1) }                 # cumulative adjustment for multi-arm trials
  }   

d[1]<-0
for(k in 2:NT){
   d[k] ~ dnorm(0,hyperVarInNormMean)
}                       #  vague priors for basic parameters

sd ~ dunif(0,hyperSDInUnif)                                  #  vague prior for random effects standard deviation 
tau <-1/pow(sd,2)
	
# Absolute log odds(success) on Treatment A,  based on a seperate model on the
#  6 trials Treatment A arms. 
# mA ~ dnorm(-0.015322905,1.694339772)
  mA ~ dnorm(meanmA,precmA)
# Absolute pr(success) Treatments B,C,D based on T[1] and the 
#   MEAN  Relative treatment effects 
for(k in 1:NT){ 
   logit(T[k]) <- mA  +d[k] 
}	

# ranking
rk <- rank(T[]) 
for(k in 1:NT){ 
#rk[k]<-NT+1 - rank(T[],k)
    best[k] <- equals(rk[k],1)
}

# pairwise ORs
for(c in 1:(NT)){
  for(k in 1:NT){
      lor[c,k] <- d[k] - d[c]
      log(or[c,k]) <- lor[c,k]
  }
}

}